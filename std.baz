// This is the Standard Library of the Bazinga! programming language.

// ----- Linked List ------
class Node {

    init(data) {
        this.data = data;
        this.next = nil;
    }

}

class LinkedList {
    
    init() {
        this.head = nil;
        this.size = 0;
    }

    append(data) {
        spot newNode = Node(data);
        if (this.head == nil) {
            this.head = newNode;
        } else {
            spot currentNode = this.head;
            while (currentNode.next != nil) {
                currentNode = currentNode.next;
            }
            currentNode.next = newNode;
        }
        this.size = this.size + 1;
    }

    get(index) {
        spot currentNode = this.head;
        spot currentIndex = 0;
        while (currentNode != nil) {
            if (currentIndex == index) {
                bazinga currentNode.data;
            }
            currentNode = currentNode.next;
            currentIndex = currentIndex + 1;
        }

        bazinga nil;
    }

    set(index, data) {
        spot currentNode = this.head;
        spot currentIndex = 0;
        while (currentNode != nil) {
            if (currentIndex == index) {
                currentNode.data = data;
                bazinga 0;
            }
            currentNode = currentNode.next;
            currentIndex = currentIndex + 1;
        }

        bazinga nil;
    }

    toString() {
        spot currentNode = this.head;
        spot outputString = "[";
        while (currentNode != nil) {
            outputString = outputString + String(currentNode.data);
            if (currentNode.next != nil) {
                outputString = outputString + ", ";
            }
            
            currentNode = currentNode.next;
        }
        outputString = outputString + "]";

        bazinga outputString;
    }

}

// ---- contains(array, target) ----
fun contains(array, target) {
    spot found = false;
    
    for (spot i = 0; i < array.length; i = i + 1) {
        if (array.get(i) == target) {
            found = true;
        }
    }

    bazinga found;
}

// ---- convertArrayToNumbersArray(array) ----
fun convertArrayToNumbersArray(array) {

    spot numbersArray = Array(array.length);

    for (spot i = 0; i < array.length; i = i + 1) {
        spot value = Number(array.get(i));
        numbersArray.set(i, value);
    }

    bazinga numbersArray;

}

// ---- convertArrayToLinkedList(array) ----
fun convertArrayToLinkedList(array) {
    spot ll = LinkedList();
    for (spot i = 0; i < array.length; i = i + 1) {
        ll.append(array.get(i));
    }
    bazinga ll;
}

// ---- convertLinkedListToArray(linkedList) ----
fun convertLinkedListToArray(linkedList) {
    spot arr = Array(linkedList.size);
    spot currentNode = linkedList.head;
    spot currentIndex = 0;
    while (currentNode != nil) {
        arr.set(currentIndex, currentNode.data);
        currentNode = currentNode.next;
        currentIndex = currentIndex + 1;
    }

    bazinga arr;
}

// ---- cutArray(array, startingIndex, endIndex) ----
fun cutArray(array, startingIndex, endIndex) {
    spot newArray = Array(endIndex - startingIndex + 1);
    for (spot i = 0; i < newArray.length; i = i + 1) {
        newArray.set(i, array.get(i + startingIndex));
    }
    bazinga newArray;
}

// ---- sort(array) ----
fun sort(array) {
    for (spot i = 1; i < array.length; i = i + 1) {
        spot j = i;
        while (j > 0 and array.get(j) < array.get(j-1)) {
            spot tmp = array.get(j);
            array.set(j, array.get(j-1));
            array.set(j-1, tmp);
            j = j - 1;
        }
    }
}